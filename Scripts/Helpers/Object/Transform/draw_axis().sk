//---------------------------------------------------------------------------------------
// Draw the Transform using 3 arrows that are colored the same as the UE4 axis scheme.
// X - Red, Y - Green, Z - Blue
//
// Params:
// @param duration      The duration to draw the arrows
// @param length        The length of the arrows
// @param size          The size of the arrows
// @param thickness     The thickness of the arrows
//
// Examples: 
// some_transform.draw_axis                       // draw using defaults
// some_transform.draw_axis(5.0)                  // draw using defaults for 5 seconds
// some_transform.draw_axis(2.0, size: 5.0)       // draw for 2 seconds with size of 5
// some_transform.draw_axis(3.0, 50.0, 5.0, 10.0) // Override all default values
//---------------------------------------------------------------------------------------

(Real duration : 0.0, Real length : 20.0, Real size : 1.0, Real thickness : 0.0)
[
  SystemLib.draw_debug_arrow(, 
    @translation, 
    @translation + unit_axis_x * length,
    size,
    Color!red,
    duration,
    thickness)

  SystemLib.draw_debug_arrow(, 
    @translation, 
    @translation + unit_axis_y * length,
    size,
    Color!green,
    duration,
    thickness)

  SystemLib.draw_debug_arrow(, 
    @translation, 
    @translation + unit_axis_z * length,
    size,
    Color!blue,
    duration,
    thickness)
]